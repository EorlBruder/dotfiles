#!/bin/zsh
# Licensed under AGPL 3.0 by EorlBruder

# TODO Satzung-style
# TODO Write completion definition
# TODO Add option for different themes/templates
#   - Give the image and the text as parameters
#   - Give the header-file as parameter
#   - Give font as parameter
# TODO Internationalize help-message

# Constants
CONF_DIR=~/.config/md2pdf
HELP_MESSAGE="This tool generates a PDF from a markdown-file. It takes various options, for example a theme.

Usage: md2pdf [options] [markdown-files]

The created file will by default be named 'basename'-final.pdf. You can use multiple Markdown-files, which will be rendered one after the other. The basename of the first-markdown file ist used for the pdf. By default a table-of-contents will be generated and the file will be in one column 

Options:
  --presentation|-p:    Generates a presentation from the document. Draft, two-columns, title-page, no-header and short options won't do anything aspect-ratio  only works in this mode. 
  --aspect-ratio:       Set the aspect-ratio. Default is 169 (for 16:9). Furhter options are in accordance with beamers config: https://pandoc.org/MANUAL.html#variables-for-beamer-slides
  --no-footer:          Generates a document without a footer
  --no-header:          Generates a document without a header
  --draft|-d:           Generates the document with a draft-watermark. The generated file will be named 'basename'-draft.pdf
  --no-toc:             Don't generate a table-of-contents
  --two-columns|-2:     Generate the file in two columns instead of one
  --title-page:         Generate a title-page
  --language|-l:        Sets the document language. Currently only works for [english, german]. Default is german
  --theme|-t <theme>:   Sets the theme to be used. Themes lay in $CONF_DIR. They can provide a logo and a text to be displayed under the logo. Default them only prints the authors name in the upper left corner.
  --newsletter:         Generates as a themed newsletter. This will put all sections in boxes and sets the header to a fixed name with boxes.
  --short|-s:           Generates a short layout. This sets title and author in the header of the first page and not in the text.
  --title:              Set the title of the document. This is not necessary, if you've set the title in the source-file, but for example when converting an odt this is required.
  --author:             Set the author of the document. This is not necessary, if you've set the author in the source-file.
  --date:               Set the date of the document. This is not necessary, if you've set the date in the source-file.
  --no-automatic-date:  Don't automatically set the current date, if no date is provided in the metadata. 
  --diff:               Creates diff of the base file with the old, provided file. Automatically sets document as a draft. 
  --debug-latex:        Prints the resulting latex-file to stdout
  --verbose:            Show verbose output
  --help|-h:            Show this message

Licensed under AGPL 3.0 by EorlBruder"

# Variables
draftOption="final"
tableOfContentsOption=--toc
columnOption=onecolumn
titlePageOption=notitlepage
language=german
languageCode=de-DE
locale=de_DE.UTF-8
themeName=default
shortOption=
titleOption=
authorOption=
dateOption=
presentation=false
aspectRatio=169
noFooterOption=
noHeaderOption=
debugLatex=false
automaticDate=true
newsletterOption=
numberSectionsOption=-N
verboseOption=
diffFile=

while [ ! -z "$1" ]; do
  case "$1" in
    --draft|-d)
      echo "Generating a draft document"
      draftOption="draft"
      ;;
    --no-toc)
      echo "Not generating a toc"
      tableOfContentsOption=
      ;;
    --two-column|-2)
      echo "Generating two columns"
      columnOption=twocolumn
      ;;
    --title-page)
      echo "Generating a title page"
      titlePageOption="titlepage"
      ;;
    --language|-l)
      shift
      if [[ "$1" = "german" || "$1" = "deutsch" || "$1" = "de" || "$1" = "DE" ]]; then
        language="german"
        locale=de_DE.UTF-8
        languageCode=de-DE
      elif [[ ("$1" = "english" || "$1" = "en" || "$1" = "EN") ]]; then
        language="english"
        locale=en_GB.UTF-8
        languageCode=en-GB
      else 
        echo "Unknown language $1"
        exit 1
      fi
      echo "Setting language to $language"
      ;;
    --theme|-t)
      shift
      echo "Using theme $1"
      themeName=$1
      ;;
    --newsletter)
      echo "Generating a newsletter"
      newsletterOption=(-H $CONF_DIR/newsletter.tex)
      tableOfContentsOption=
      columnOption=twocolumn
      numberSectionsOption=
      ;;
    --short|-s)
      echo "Using short layout"
      shortOption=short
      ;;
    --title)
      shift
      echo "Setting title as '$1'"
      titleOption=(-M title:$1)
      ;;
    --author)
      shift
      echo "Setting author as '$1'"
      authorOption=(-M author:$1)
      ;;
    --date)
      shift
      echo "Setting date as '$1'"
      dateOption=(-M date:$1)
      ;;
    --diff)
      shift
      echo "diffing with old file '$1'"
      diffFile=$1
      draftOption="draft"
      ;;
    --presentation|-p)
      echo "Generating a presentation"
      presentation=true
      ;;
    --aspect-ratio)
      shift
      echo "Setting aspect ratio as '$1'"
      aspectRatio=$1
      ;;
    --no-footer)
      echo "Generating document without a footer"
      noFooterOption=nofooter
      ;;
    --no-header)
      echo "Generating document without a header"
      noHeaderOption=noheader
      ;;
    --no-automatic-date)
      echo "Not inserting current date"
      automaticDate=false
      ;;
    --debug-latex)
      echo "Printing Latex-File"
      debugLatex=true
      ;;
    --verbose)
      verboseOption=--verbose
      ;;
    --help|-h)
      echo $HELP_MESSAGE
      exit 0
      ;;
    -*|--*)
      echo "Unknown option. Displaying help-message:"
      echo $HELP_MESSAGE
      exit 1
      ;;
    *)
      filename+=("$1")
      if [ -z $basename ]; then
        basename="$1:r"
      fi
      ;;
  esac
shift
done

if [ -z $basename ]; then
  echo "No file provided"
  echo $HELP_MESSAGE
  exit 1
fi

headerTheme=$CONF_DIR/$themeName-header.tex
preprocessedFile=/tmp/md2pdf-preprocessed.md

defaultOptions=( \
  -M lang:$languageCode \
  -M csquotes:true \
  -M include-auto \
  -V classoption:$language \
  -V urlcolor:red \
  $titleOption \
  $authorOption \
  $dateOption \
  $tableOfContentsOption \
  --pdf-engine=xelatex \
  -H $CONF_DIR/packages.tex \
  -H $CONF_DIR/utils.tex \
  -H $headerTheme \
  -H $CONF_DIR/localization.tex \
  -H $CONF_DIR/main.tex \
  $newsletterOption \
  -L $CONF_DIR/fences-filter.lua \
  -L $CONF_DIR/criticmarkup.lua \
  -L $CONF_DIR/include-files.lua \
  $verboseOption \
  $preprocessedFile)

if [ $automaticDate = true ]; then
  defaultOptions+=(-L $CONF_DIR/currentdate-filter.lua)
fi

if [ $presentation = true ]; then
  echo "Converting $filename to PDF, creating $basename-slides.pdf"
  modeOptions=(-t beamer \
    -M theme:pureminimalistic \
    -V aspectratio:$aspectRatio \
    -V themeoptions:customfont \
    -V themeoptions:showmaxslides \
    -V themeoptions:$noFooterOption \
    -H $CONF_DIR/slides.tex)
  outputOption=(-o "$basename-slides.pdf")
else   
  echo "Converting $filename to PDF, creating $basename-$draftOption.pdf"
  modeOptions=($numberSectionsOption \
    -V secnumdepth:4 \
    -V classoption:$draftOption \
    -V classoption:$columnOption \
    -V classoption:$titlePageOption \
    -V classoption:$shortOption \
    -V classoption:$noFooterOption \
    -V classoption:$noHeaderOption \
    -H $CONF_DIR/document.tex)
  outputOption=(-o "$basename-$draftOption.pdf")
fi

if [ $debugLatex = true ]; then
  outputOption=(--to latex)
fi

if [ $diffFile ]; then 
  diffedFile=/tmp/md2pdf-diffed.md
  pandiff $diffFile $filename > $diffedFile 
  filename=$diffedFile
fi

pancritic $filename -m m --critic-template /dev/null -t latex -o $preprocessedFile

if [ $diffFile ]; then 
  rm $diffedFile
fi

env LANG=$locale pandoc $defaultOptions $modeOptions -H $CONF_DIR/final-hook.tex $outputOption
rm $preprocessedFile
