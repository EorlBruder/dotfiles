#!/bin/zsh
# Licensed under AGPL 3.0 by EorlBruder

# TODO Rename, because this does also work for stuff different from Markdown
# TODO Add quick-draft-mode
#   - If quick-draft-mode is set, it will generate the document without links and images. Watermark and footer will be set to draft
#   - If normal-draft-mode is set only watermark and footer will be set to draft
# TODO Write completion definition
# TODO Allow multiple input files
# TODO Add option for different themes/templates
#   - Give the image and the text as parameters
#   - Give the header-file as parameter
#   - Give font as parameter
# TODO Internationalize help-message

# Constants
HELP_MESSAGE="This tool generates a PDF from a markdown-file. It takes various options, for example a theme.

Usage: md2pdf [options] [markdown-file]

The created file will by default be named 'basename'-final.pdf. By default a table-of-contents will be generated and the file will be in one column 

Options:
  --presentation|-p:  Generates a presentation from the document. Draft, two-columns, title-page and short options won't do anything. Aspect-Ratio and no-footer only work in this mode. 
  --aspect-ratio:     Set the aspect-ratio. Default is 169 (for 16:9). Furhter options are in accordance with beamers config: https://pandoc.org/MANUAL.html#variables-for-beamer-slides
  --no-footer:        Generates a presentation without a footer
  --draft|-d:         Generates the document with a draft-watermark. The generated file will be named 'basename'-draft.pdf
  --no-toc|-n:        Don't generate a table-of-contents
  --two-columns|-2:   Generate the file in two columns instead of one
  --title-page:       Generate a title-page
  --language|-l:      Sets the document language. Currently only works for [english, german]. Default is german
  --theme|-t <theme>: Sets the theme to be used. Themes lay in ~/.config/md2pdf. They can provide a logo and a text to be displayed under the logo. Default them only prints the authors name in the upper left corner.
  --short|-s:         Generates a short layout. This sets title and author in the header of the first page and not in the text.
  --title:            Set the title of the document. This is not necessary, if you've set the title in the source-file, but for example when converting an odt this is required.
  --author:           Set the author of the document. This is not necessary, if you've set the author in the source-file.
  --date:             Set the date of the document. This is not necessary, if you've set the date in the source-file.
  --help|-h:          Show this message

Licensed under AGPL 3.0 by EorlBruder"

# Variables
draftOption="final"
tableOfContentsOption=--toc
columnOption=onecolumn
titlePageOption=notitlepage
language=german
languageCode=de-DE
themeName=default
shortOption=
titleOption=
authorOption=
dateOption=
presentation=false
aspectRatio=169
noFooterOption=

while [ ! -z "$1" ]; do
  case "$1" in
    --draft|-d)
      echo "Generating a draft document"
      draftOption="draft"
      ;;
    --no-toc|-n)
      echo "Not generating a toc"
      tableOfContentsOption=
      ;;
    --two-column|-2)
      echo "Generating two columns"
      columnOption=twocolumn
      ;;
    --title-page)
      echo "Generating a title page"
      titlePageOption="titlepage"
      ;;
    --language|-l)
      shift
      echo "Setting language to $1"
      language="$1"
      if [ "$1" = "german" ]; then
        languageCode=de-DE
      elif [ "$1" = "english" ]; then
        languageCode=en-GB
      fi
      ;;
    --theme|-t)
      shift
      echo "Using theme $1"
      themeName=$1
      ;;
    --short|-s)
      echo "Using short layout"
      shortOption=short
      ;;
    --title)
      shift
      echo "Setting title as '$1'"
      titleOption=(-M title:$1)
      ;;
    --author)
      shift
      echo "Setting author as '$1'"
      authorOption=(-M author:$1)
      ;;
    --date)
      shift
      echo "Setting date as '$1'"
      dateOption=(-M date:$1)
      ;;
    --presentation|-p)
      echo "Generating a presentation"
      presentation=true
      ;;
    --aspect-ratio)
      shift
      echo "Setting aspect ratio as '$1'"
      aspectRatio=$1
      ;;
    --no-footer)
      echo "Generating presentation without footer"
      noFooterOption=nofooter
      ;;
    --help|-h)
      echo $HELP_MESSAGE
      exit 0
      ;;
    -*|--*)
      echo "Unknown option. Displaying help-message:"
      echo $HELP_MESSAGE
      exit 1
      ;;
    *)
      filename="$1"
      basename="$1:r"
      ;;
  esac
shift
done

if [ -z $filename ]; then
  echo "No file provided"
  echo $HELP_MESSAGE
  exit 1
fi

headerTheme=~/.config/md2pdf/$themeName-header.tex

defaultOptions=(-V classoption:$language \
  -V urlcolor:red \
  $titleOption \
  $authorOption \
  $dateOption \
  $tableOfContentsOption \
  --pdf-engine=xelatex \
  -H $headerTheme \
  -H ~/.config/md2pdf/md2pdf.tex \
  -L ~/.config/md2pdf/fences-filter.lua \
  "$filename")

if [ $presentation = true ]; then
  echo "Converting $filename to PDF, creating $basename-slides.pdf"
  pandoc \
    $defaultOptions \
    -t beamer \
    -M theme:pureminimalistic \
    -V aspectratio:$aspectRatio \
    -V themeoptions:customfont \
    -V themeoptions:showmaxslides \
    -V themeoptions:$noFooterOption \
    -H ~/.config/md2pdf/md2pdf-slides.tex \
    -o "$basename-slides.pdf"
else   
  echo "Converting $filename to PDF, creating $basename-$draftOption.pdf"
  pandoc \
    $defaultOptions \
    -N \
    -V secnumdepth:4 \
    -V classoption:$draftOption \
    -V classoption:$columnOption \
    -V classoption:$titlePageOption \
    -V classoption:$shortOption \
    -H ~/.config/md2pdf/md2pdf-doc.tex \
    -o "$basename-$draftOption.pdf" 
fi
